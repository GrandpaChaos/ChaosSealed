Draft = require \./draft
Sock = require \./sock

drafts = {}

rm = ->
  delete drafts[@id]

join = (opts={}) ->
  {id, name, room, zone} = opts
  name ?.= slice 0 16
  unless zone of <[ main side ]>
    zone = \main
  @ <<<< { id, name, room, zone }

  if drafts[room]
    that.join @
  else
    @send \error 'room not found'

router =
  create: (opts) ->
    draft = new Draft opts
      &on \end rm
      drafts[&id] = &
    draft.id
  connect: (ws) ->
    sock = new Sock ws
    sock.once \join join

module.exports = router
