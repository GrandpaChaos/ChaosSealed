fs = require \fs

request = require \request

files = require \./files

{Cards, Sets} = files

try
  raw = require \../../data/AllSets
  go raw
catch
  (err, res, body) <- request \http://mtgjson.com/json/AllSets.json
  throw err if err
  raw = JSON.parse body
  go raw

go = (raw) ->
  raw.TSP.cards = <>concat raw.TSB.cards
  delete raw.TSB

  raw.PLC.booster = [\common] * 11
  raw.FUT.booster = [\common] * 11

  raw.UGL.cards = for card of <>
    if !/token card/test card.name
      card

  for tla, rawSet in raw
    continue if rawSet.type of <[ archenemy box vanguard ]>

    cards = []
    split = []
    prev = null

    for rawCard of rawSet.cards
      if rawCard.layout of <[ double-faced flip ]>
      and rawCard.number[* - 1] is \b
        continue

      {colors, multiverseid, name, rarity} = rawCard

      rarity .= toLowerCase!
      if rarity is 'basic land'
        continue
      if rarity is 'mythic rare'
        rarity = \mythic

      if name is prev
        continue # alt art
      prev = name

      if rawCard.layout is \split
        name = rawCard.names.join ' // '

      url = "http://mtgimage.com/multiverseid/#{multiverseid}.jpg"

      color =
        if !colors
          \A
        else if colors.length > 1
          \Y
        else if colors.0 is \Blue
          \U
        else
          colors.0.0

      card = { color, name, rarity, url }
        <<<< rawCard{ cost: manaCost, cmc, text, type }
      card.cmc or= 0

      if rawCard.layout is \split
        split.push card
      else
        cards.push card

    join = {}
    for splitCard of split
      {name} = splitCard
      card = join[name]

      if !card
        join[name] = splitCard
        cards.push splitCard
        continue

      card.cmc  += splitCard.cmc
      card.text += "\n\n//\n\n#{splitCard.text}"
      card.cost += " // #{splitCard.cost}"

      if card.color is not splitCard.color
        card.color = \Y

    size = 0
    if rawSet.booster
      size = (x if x is \common for x of that)length
    files.add tla, cards, size

  for card of raw.HHO.cards
    # these cards have no multiverse id
    {name} = card
    lc = (card.names?join(' // ') or name)toLowerCase!
    Cards[lc].urls['HHO'] = "http://mtgimage.com/card/#name.jpg"

  {DGM} = Sets
  DGM.special =
    gate:
      'azorius guildgate'
      'boros guildgate'
      'dimir guildgate',
      'golgari guildgate'
      'gruul guildgate'
      'izzet guildgate'
      'orzhov guildgate'
      'rakdos guildgate'
      'selesnya guildgate'
      'simic guildgate'
    shock:
      'blood crypt'
      'breeding pool'
      'godless shrine'
      'hallowed fountain'
      'overgrown tomb'
      'sacred foundry'
      'steam vents'
      'stomping ground'
      'temple garden'
      'watery grave'
      "maze's end"
  DGM.common = for name of <> => name unless name of DGM.special.gate
  DGM.mythic = for name of <> => name unless name is "maze's end"

  files.write!
