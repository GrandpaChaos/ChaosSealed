fs = require \fs

_ = require \../_
{Cards, mws} = require \../../data

module.exports = (players, cube) ->
  nPacks = players * cube.packs
  nCards = nPacks * cube.cards
  names = _.choose nCards, cube.list
  cards = for name of names
    card = {}<<<<Cards[name]
    code = Object.keys(card.set)0
    card.code = mws[code] or code
    card{rarity, url} = card.set[code]
    delete card.set
    card
  packs = []

  for i from 0 til nPacks
    pack = []
    for j from 0 til cube.cards
      pack.push cards.pop!
    packs.push pack

  packs
