fs = require \fs

util = require \../util

{Cards, Sets} = require \../../data

rares =
  special: 0
  mythic: 1
  rare: 2
  uncommon: 3
  common: 4

module.exports = self =
  Cards: Cards
  Sets: Sets

  clear: ->
    self.Cards = Cards := {}
    self.Sets  = Sets  := {}

  add: (tla, cards, size) ->
    set =
      common: []
      uncommon: []
      rare: []
      mythic: []
      special: []
      size: size

    for card of cards
      card.score = 99
      card.name = util.name card.name
      lc = card.name.toLowerCase!

      [rarity, url] = delete card<[rarity url]>

      set[rarity]push lc
      rarity = rares[rarity]

      if Cards[lc]
        that.set[tla] = { rarity, url }
      else
        card.set = "#tla": { rarity, url }
        Cards[lc] = card

    return unless size

    for rarity of <[ mythic special ]>
      unless set[rarity]length
        delete set[rarity]

    Sets[tla] = set

  write: ->
    fs.writeFileSync \data/cards.json JSON.stringify Cards, null 2
    fs.writeFileSync \data/sets.json  JSON.stringify Sets,  null 2
