Cards = require \../../data/cards
Sets  = require \../../data/sets
{choose, rand} = require \../_

genPack = (code) ->
  {size, common, uncommon, rare, mythic, special} = Sets[code]
  if mythic and not rand 8
    rare = mythic

  pack = []concat do
    choose size, common
    choose 3 uncommon
    choose 1 rare

  switch code
  case "Dragon's Maze"
    special = if rand 20 then <>gate else <>shock
  case 'Modern Masters'
    special = []concat common, uncommon, rare

  if special
    index = rand special.length
    pack.push special[index]

  for name of pack
    card = {}<<<<Cards[name]
    card.code = code
    card.url = card.urls[code]
    card

genPacks = (count, sets) ->
  packs = []
  for set of sets
    for i til count
      packs.push genPack set
  packs.reverse!

module.exports = genPacks
